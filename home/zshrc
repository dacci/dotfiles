## .zshrc

OS=`uname -s`

## Prompts
setopt prompt_subst
setopt no_prompt_cr
PROMPT="[%n@%m %1~]%(!.#.$) "
RPROMPT=' %~'

## Key bindings
bindkey -v              # vi key bindings
bindkey -e              # emacs key bindings
bindkey ' ' magic-space # also do history expansion on space
bindkey "^?" backward-delete-char
bindkey "^H" backward-delete-char
bindkey "^[[3~" delete-char
bindkey "^[[1~" beginning-of-line
bindkey "^[[4~" end-of-line

## Completion
autoload -U compinit
compinit -u

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' list-colors ''

# Shift+Tab to complete in reverse order
bindkey "^[[Z" reverse-menu-complete

# Load cached hosts
[ -r ~/.ssh/known_hosts ] && _cache_hosts=($(cut -d ' ' -f 1 ~/.ssh/known_hosts | sed -e 's/,/ /'))

# Enable extended glob
setopt extended_glob

## History
setopt extended_history append_history
setopt hist_ignore_dups hist_ignore_space
setopt inc_append_history share_history

HISTSIZE=100000
HISTFILESIZE=100000
SAVEHIST=100000
HISTFILE=~/.zsh_history

## Terminal
# Disable C-s
setopt no_flow_control

echo_caption() {
  if [[ "$TERM" =~ '([xk]term|rxvt|screen.*)' ]]; then
    print -nP "\e]0;$1\\C-g"
  fi
}

chpwd() {
  if [[ "$TERM" =~ 'screen.*' ]]; then
    screen -qX chdir "`pwd`" >/dev/null 2>&1
  fi
}

precmd() {
  echo_caption %n@%m:%~

  if [[ "$TERM" =~ 'screen.*' ]]; then
    echo -ne "\ekzsh\e\\"
  fi
}

preexec() {
  if [[ "$TERM" =~ 'screen.*' ]]; then
    local -a cmd; cmd=(${(z)1})
    echo -ne "\ek$cmd[1]\e\\"
  fi
}

## Misc
# Use hard limits, except for a smaller stack and no core dumps
if [[ ! "$OS" =~ 'CYGWIN*' ]]; then
  unlimit
  limit stack 8192
  limit core 0
  limit -s
fi

# Autoload all shell functions
for func in $^fpath/*(N-.x:t); autoload $func

# Automatically remove duplicates from these arrays
typeset -U path cdpath fpath manpath

# Directory Stack
DIRSTACKSIZE=16
setopt auto_pushd pushd_minus pushd_silent pushd_to_home pushd_ignore_dups
alias gd='dirs -v; echo -n "select number: "; read newdir; cd -"$newdir"'

# Shell Options
setopt rm_star_silent
setopt auto_cd
setopt ignore_eof
setopt print_eight_bit      # multi character byte
setopt no_beep
setopt noclobber

[ -z "$LS_COLORS" ] && command -v dircolors >/dev/null && eval `dircolors -b`

autoload zmv
#alias zmv='noglob zmv -W'

# User specific aliases and functions
[ -f ~/.zalias ] && source ~/.zalias

unset OS
